
AC_REVISION($Revision: 1.1 $)
AC_INIT(dfs/client/storage-layer.jl)

dnl Release versioning info
version="0.0"

dnl Find the system type; this isn't crucial
AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_GCC_TRADITIONAL
AC_ISC_POSIX
AC_PROG_RANLIB

dnl If using GCC and it doesn't look as though the cflags have been
dnl set explicitly, add some warning options
if test "x${GCC}" = "xyes" -a "x$CFLAGS" = "x-g -O2"; then
  CFLAGS="${CFLAGS} -Wall -Wmissing-prototypes"
fi

dnl Checks for libraries.

AM_PATH_REP(0.14)
AC_MSG_CHECKING([for shared lisp directory])
  lispdir=`rep-config --lispdir`
  AC_MSG_RESULT([$lispdir])
AC_SUBST(lispdir)

dnl Checks for header files.
AC_STDC_HEADERS
AC_HAVE_HEADERS(string.h fcntl.h unistd.h)
     
dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_CHECK_FUNCS(lchown)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

dnl How do we get dependency lines in the Makefile?
if test "x${GCC}" = "xyes"; then
  MAKEDEP='$(CC) -MM -MG'
else
  case ${host} in
    *-dec-osf*)
      # works on Tru64
      MAKEDEP='$(CC) -M'
      ;;
    *-sun-solaris*)
      # works on Solaris
      MAKEDEP='/usr/lib/cpp -M'
      ;;
    *)
      # disable dependences?
      MAKEDEP='true'
      ;;
  esac
fi
AC_SUBST(MAKEDEP)

dnl Nonstandard exported symbols
AC_SUBST(version)
AC_SUBST(repexecdir)
AC_SUBST(CPPFLAGS)
AC_SUBST(CFLAGS)
AC_SUBST(LIBS)

AC_OUTPUT([Makefile])

dnl Local variables:
dnl major-mode: sh-mode
dnl End:
